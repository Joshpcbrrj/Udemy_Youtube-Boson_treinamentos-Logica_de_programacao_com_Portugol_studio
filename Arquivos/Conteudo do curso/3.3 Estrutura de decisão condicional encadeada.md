# Estrutura de decisão condicional encadeada (Se, Senão se, Senão)

## Desvio aninhado

Dado por varias condições para escolha da mais adequada.
- Verifica condições sucessivas para enquadrar a tomada de decisão do caso.
- A última condição é uma negação de todas as outras. Caso o programa não se enquadre em nenhuma das posteriores, agirá conforme essa (Cadeia começa com o “se” e termina com o ”senão”).

>**OBS:** Entre a primeira condição(SE) e última(SENÃO) podemos usar quantos (SE SENÃO) forem necessários para expressar a lógica que queremos implementar.

### Sintaxe
```portugol
se(1° Condição){

	instruções caso a condição 1 retorne verdadeiro.
}
Senao se(2° condição) {
	instruções caso a 1 condição falsa e a 2 verdadeira

}senão se(3° condição) {
	instruções caso a 1 e 2 condição forem falsas e a 3 condição verdadeira
}
senão{
	instruções caso todas as condições forem falsas.
}
instruções após a verificações de ses e senão
```

#### Exemplo de fluxograma
<img src="/Arquivos/img/25.jpg" alt="Texto Alternativo" width="550">

<br>

<br>

## Projetos teste 

### Aplicando o fluxograma acima pra calcular média com (Se, senão se, senão):

<details>
<summary>Código</summary>

```portugol
programa
{
	real nota_1, nota_2, nota_3 //Variáveispara guardar as notas do trimestre.
	real media //Variável para guardar a média geral nos 3 semetres.
	
	funcao inicio()
	{
		escreva("Entre com a primeira nota do aluno: ")
		leia(nota_1)//Pegando a primeira nota.
		
		escreva("Entre com a segunda nota do aluno: ")
		leia(nota_2)//Pegando a segunda nota.

		escreva("Entre com a terceira nota do aluno: ")
		leia(nota_3)//Pegando a segunda nota.

		media = (nota_1 + nota_2 + nota_3) / 3 //Guardando a média das 3 notas.

		se(media >= 7.0) {//Condição para aprovar o aluno (1° teste lógico)
			escreva("Resultado: Aprovado, chefe ! \n") 
		}
		senao se (media >= 5.0) {//Condição para a recuperação do aluno(2° teste lógico)
			escreva("Em recuperação ! Calma que ainda da. \n")
		}
		senao {//Condição para a reprovação do do aluno (caso todas as outras retornem falso)
			escreva("Reprovado. Esse ano não deu, fera. Mas ano que vem tem mais \n")
		}
				
		escreva("Sua média foi: ", media,"\n")//Valor total da média
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20calculo%20de%20m%C3%A9dia%20com%20decis%C3%A3o%20aninhada%20(Se%2C%20Sen%C3%A3o%20se%2C%20sen%C3%A3o).por)

<br>

### Escrever um programa que calcule a média e a quantidade de faltas (Faltas devem ser menor que 10).

<details>
<summary>Código</summary>

```portugol
programa
{
	real nota_1, nota_2, nota_3 //Variáveispara guardar as notas do trimestre.
	real media //Variável para guardar a média geral nos 3 semetres.
	inteiro faltas
	
	funcao inicio()
	{
		escreva("Entre com a primeira nota do aluno: ")
		leia(nota_1)//Pegando a primeira nota.
		
		escreva("Entre com a segunda nota do aluno: ")
		leia(nota_2)//Pegando a segunda nota.

		escreva("Entre com a terceira nota do aluno: ")
		leia(nota_3)//Pegando a segunda nota.

		escreva("Entre com a quantidade de faltas: ")
		leia(faltas)//Pegando quantidade de faltas

		media = (nota_1 + nota_2 + nota_3) / 3 //Guardando a média das 3 notas.
		
		se(faltas >= 10) {
			escreva("\nDesculpe, mas independente da nota você esta reprovado por falta. Quem não é visto não é lembrado \n")
		}
		senao {
			escreva("\nVc esta dentro da média de presença exigida.\n")
		}
		escreva ("Sua quantida de faltas foi: ", faltas,"\n")
		

		se((media >= 7.0) e (faltas < 10)) {//Condição para aprovar o aluno (1° teste lógico)
			escreva("Resultado: Aprovado, chefe ! \n") 
		}
		senao se ((media >= 5.0) e (faltas < 10)) {//Condição para a recuperação do aluno(2° teste lógico)
			escreva("Em recuperação ! Calma que ainda da. \n")
		}
		senao {//Condição para a reprovação do do aluno (caso todas as outras retornem falso)
			escreva("Reprovado. Esse ano não deu, fera. Mas ano que vem tem mais \n")
		}
				
		escreva("Sua média foi: ", media,"\n")//Valor total da média
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20desafio%20de%20calculo%20de%20m%C3%A9dia%20com%20decis%C3%A3o%20aninhada%20(Se%2C%20sen%C3%A3o%20se%2C%20sen%C3%A3o).por)

<br>

<br>

[Voltar ao inicio](/README.md)