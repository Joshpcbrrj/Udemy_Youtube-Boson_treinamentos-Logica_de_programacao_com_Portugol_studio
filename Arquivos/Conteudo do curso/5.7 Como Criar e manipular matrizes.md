# Como criar e manipular Matrizes (Vetores bi dimensionais)

## Conceituando matriz
- Em matemática, uma matriz (m por n) é um conjunto de elementos dispostos na linha ( m ) e colunas( n ).
- São vetores de 2 dimensões (Linhas e colunas), lembrando uma tabela. Algo como vetores interligados.
- Na programação costumamos usar ( i ) para indicar linha e ( j ) para indicar a coluna do elemento.
- A matriz geralmente é representada por uma letra maiúscula.

### Representação de matriz na matemática
<img src="/Arquivos/img/41.jpg" alt="Texto Alternativo" width="700">

### Na computação usa-se matriz para diversos tipos de representações

Exemplos:
- Representação de imagens.
- Dados para plotagem de gráficos.
- Mapeamento e cálculos de distância.
- Jogos 2d
- Implementação de estruturas  de dados especiais.
- Cálculos tabulares (Como em planilhas).
    Entre outras finalidades

### Entendendo a contagem usada na matriz na programação

>**OBS**
Note, a numeração de linhas e colunas se inicia em zero. Portanto, a última posição será sempre n -1.
Note, sempre vamos começar a contar a linha e depois a coluna, para identificar a posição de um dado

<img src="/Arquivos/img/42.jpg" alt="Texto Alternativo" width="400">

#### Identificando posições por valores:
O valor 22 está na posição ( 0,0 ).
O valor 15 está na posição ( 2,1 ).
O valor 32 está na posição ( 1,2 ).
O valor 96 está na posição ( 3,2 ).

### Sintaxe para a declaração de matriz no Portugol studio
```portugol
tipo_de_dado nome_da_matriz[x][y]
/*OBS:*/ 
// x e y correspondem ao número de linhas e colunas da  matriz, repectivamente.
// A quantidade de objetos que podem entrar na matrizserá resultado de (x * y).
```
>**OBS:** 
Também podemos declarar matrizes com constantes
> ```portugol
> const inteiro x = 5
> const inteito y = 3
>
> inteiro matriz[x][y]
>```

### Podemos acessar ou atribuir dado de a posição específica da matriz
```portugol
//Inserindo
nome_da_matriz[pos_em_x] [pos_em_y] = valor_que_queremos_inserir

//escrevendo na tela
escreva(nome_da_matriz[pos_em_x] [pos_em_y])
```
<br>

## Projetos teste

### Crie um programa que permita que o usuário insira 9 números e transforme em uma matriz 3 por 3 e mostre na tela
<details>
<summary>Código</summary>

```portugol
programa
{
	inteiro matriz[3][3]
	inteiro linha
	inteiro coluna
	
	funcao inicio()
	{
		//Preenchendo a matriz com dados (Para cada linha eu vou preencher todas as colunas) laço dentro de laço
		para(linha = 0; linha < 3; linha ++){
			para(coluna = 0; coluna <= 2; coluna ++){
				escreva("Ditite um número qualquer: ")
				leia(matriz[linha][coluna])
				escreva("\n")
			}
		}

		//Limpando os dados da tela 
		limpa()
		escreva("A matriz completa é:\n")
		
		//Escrever a matriz completa na tela
		para(linha = 0; linha < 3; linha ++){
			para(coluna = 0; coluna <= 2; coluna ++){
				
				//condição para organizar os números nas colunas da matriz que será apresentada na tela
				se(coluna < 2){
					escreva(matriz[linha][coluna], " |")
				}
				senao{
					escreva(matriz[linha][coluna], "\n")
				}
		
			}	
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20matriz%20com%209%20n%C3%BAmeros.por)

<br>

### Deixe que o usuário escolha 9 números, monte uma matiz e exiba os números
<details>
<summary>Código</summary>

```portugol
programa
{
	// Criando a matriz
	inteiro matriz[3][3]
	
	//Criando variáveis para ajudar no preenchimento dos dados na matriz
	inteiro lin, col
	
	funcao inicio()
	{
		// Pedindo dados para popular a matriz
		para(lin = 0; lin <=2; lin++){
			para(col = 0; col <= 2; col++){
				escreva("Digite um número: ")
				leia(matriz[lin][col])
			}
		}escreva("\n")

		// Imprimindo números da matriz em tela
		para(lin = 0; lin <=2; lin++){
			para(col = 0; col <= 2; col++){
				se(col < 2){
					escreva(matriz[lin][col], ", ")	
				}
				senao{
					escreva(matriz[lin][col], "\n")
				}
			}
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20escolhendo%209%20n%C3%BAmeros%20para%20a%20matriz.por)

<br>

### Crie uma matriz 10 por 10 e preencha com números aleatórios entre 1 e 100
<details>
<summary>Código</summary>

```portugol
programa
{
	//Incluindo biblioteca "útil" para gerar números aleatórios.
	inclua biblioteca Util --> u
	
	//Criando variáveis
	inteiro matriz[10][10]
	inteiro lin, col
	inteiro soma = 0 //Variávelresponsavel pela soma de valores (acumuladora)
		
	funcao inicio()
	{
		//Laço encadeado para preenchimento da matriz com números aleatórios
		para(lin = 0; lin < 10; lin++){
			para(col = 0; col < 10; col++){
				matriz[lin][col] = u.sorteia(1, 100)
			}
		}

		//Escreve dados da matriz na tela
		para(lin = 0; lin < 10; lin++){
			para(col = 0; col < 10; col++){
				
				//condição para escrever os números da matriz de forma tabular
				se(col < 9){
					se(matriz[lin][col] < 10){
						escreva(0,0, matriz[lin][col], " |")
					}
					senao se ((matriz[lin][col] >= 10) e (matriz[lin][col] <= 99)){
						escreva(0, matriz[lin][col], " |")
					}
					senao{
						escreva(matriz[lin][col], " |")
					}
				}
				//condição para pular linha a cada 10 números
				senao{
					se(matriz[lin][col] < 10){
						escreva(0,0, matriz[lin][col], "\n")
					}
					senao se ((matriz[lin][col] >= 10) e (matriz[lin][col] <= 99)){
						escreva(0, matriz[lin][col], "\n")
					}
					senao{
						escreva(matriz[lin][col], "\n")
					}
				}
			}
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20matriz%2010%20por%2010%20com%20n%C3%BAmeros%20aleat%C3%B3rios.por)

<br>

### Crie uma matriz 10 por 10 e preencha com números aleatórios entre 1 e 100, exiba a matriz em formato tabular e faça a soma de todos os números e exiba essa soma ao final
<details>
<summary>Código</summary>

```portugol
programa
{
	//Incluindo biblioteca "útil" para gerar números aleatórios.
	inclua biblioteca Util --> u
	
	//Criando variáveis
	inteiro matriz[10][10]
	inteiro lin, col
	inteiro soma = 0 //Variávelresponsavel pela soma de valores (acumuladora)
		
	funcao inicio()
	{
		//Laço encadeado para preenchimento da matriz com números aleatórios
		para(lin = 0; lin < 10; lin++){
			para(col = 0; col < 10; col++){
				matriz[lin][col] = u.sorteia(1, 100)
			}
		}

		//Escreve dados da matriz na tela
		para(lin = 0; lin < 10; lin++){
			para(col = 0; col < 10; col++){
				
				//condição para escrever os números da matriz de forma tabular
				se(col < 9){
					escreva(matriz[lin][col], "\t")
				}
				//condição para pular linha a cada 10 números
				senao{
						escreva(matriz[lin][col], "\n")
					}	
			}
			//Distancia de uma linhade cada carreira de números a cada looping de coluna
			escreva("\n")
		}

		//Somatório de todos os números da matriz
		para(lin = 0; lin < 10; lin++){
			para(col = 0; col < 10; col++){
				soma += matriz[lin][col]
			}
		}

		//Escrevendo a soma total da  matriz na tela
		escreva("\nA soma total dos números da matriz é: ", soma, "\n")
			
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20matriz%2010%20por%2010%20e%20soma%20ao%20final.por)

<br>

<br>

[Voltar ao inicio](/README.md)

