# Como usar Bibliotecas e suas Funções no Portugol studio

## Biblioteca
É um arquivo que organiza código pré-definido para o uso em aplicações. É uma coleção de recursos usados no desenvolvimento de software. 

Pode incluir :

- Dados de configuração
- Documentação
- Procedimentos
- Classes
- Funções
- Templates
- especificações de tipo

Dentre outros recursos

### Vantagens de uso

- Possuem pre definições para programação modular.
- Rapidez no desenvolvimento de aplicação com o reuso de código.
- Evitar criação do zero de funcionalidades já conhecidas (Não reinventar a roda).
- Facilidade de atualização de funções do programa.

#### Sintaxe para incluir biblioteca no Portugol Studio
```portugol
inclua biblioteca nome_da_biblioteca --> alias_da_biblioteca
```

<br>

<br>

## Projetos teste
### Inclusão de bibliotecas e uso de funcionalidades

<details>
<summary>Código</summary>

```portugol
programa
{//Incluindo bibliotecas no escopo global
	inclua biblioteca Util --> u //Incluindo alias "u" para biblioteca "Util".
	inclua biblioteca Texto --> t //Incluindo alias "t" para biblioteca "Texto".
	inclua biblioteca Calendario --> c //Incluindo alias "c" para biblioteca "Calendario".
	inclua biblioteca Matematica --> m //Incluindo alias "mat" para biblioteca "Matematica".

	cadeia nome
	
	funcao inicio()
	{
		/*Usando elemento PI da biblioteca Matemática */
		escreva(m.PI,"\n") 

		/*Lendo e escrevendo nome digitado em caixa alta usando a biblioteca "Texto"*/
		escreva("\nDigite seu nome: ")
		leia(nome)//Armazenando nome digitado
		escreva("Retornando o nome digitado em caixa alta: ", t.caixa_alta(nome), "\n") //Passando nome como parâmetro para função "caixa_alta" 

		/*Usando a biblioteca calendario*/
		escreva("\nEstamos no dia:",c.dia_mes_atual(), "\n") 
		escreva("Do ano de:",c.ano_atual(),"\n") 

		/*Usando a biblioteca "Util" para escrever números*/
		para(inteiro i = 0; i <=10; i++) {
			escreva("\nOs números serão escritos com temporizador da LIB Util com delay de 2s pra cada novo número \n")
			escreva(i + "\n")
			u.aguarde(2000)
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20bibliotecas%20e%20funcionalidades.por)

<br>

### Programa para executar radiciação de qualquer número com qualquer índice

<details>
<summary>Código</summary>

```portugol
programa
{
	inclua biblioteca Matematica --> m
	real radicando, indice, raiz
	
	funcao inicio()
	{
		//Pedindo numero para o radicando.
		escreva("Digite um número para o radicando: ")
		leia(radicando)

		//Pedindo numero para o indice.
		escreva("Digite um número para o índice: ")
		leia(indice)

		//Retornando numeros escolhidos para a operação de radiciação.
		escreva("\nO radicando é :",radicando, "\n")
		escreva("O indice é :",indice, "\n")  

		raiz = m.raiz(radicando, indice)

		escreva ("\nA radiciação do radicando: ", radicando, " e do indice: ", indice, " e: ", raiz, "\n")
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20escolha%20radicando%20e%20indice%20para%20radicia%C3%A7%C3%A3o.por)

<br>

<br>

[Voltar ao inicio](/README.md)