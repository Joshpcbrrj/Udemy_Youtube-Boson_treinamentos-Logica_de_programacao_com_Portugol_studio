# Algoritmo de pesquisa linear em vetores

## Pesquisa sequencial em  arrays
- Na busca linear, buscamos um valor dentro de um array comparando o valor de cada posição, seguindo a sequência de posições do vetor, da primeira até a última. 

### Representação
<img src="/Arquivos/img/37.jpg" alt="Texto Alternativo" width="650">

<br>

## Projetos teste
### Crie um programa onde eu possa inserir 5 números e decidir qual número procurar dentre eles
<details>
<summary>Código</summary>

```portugol
programa
{
	inteiro num, pos, i
	inteiro vet[5]
	
	funcao inicio()
	{
		//Preenchendo o array.
		para(i = 0; i < 5; i++){
			escreva("Digite um valor: ")
			leia(vet[i])
		}

		//Valor a pesquisar
		escreva("Digite o valor que vamos pesquisar: ")
		leia(num)

		//Definindo posição inicial para "pos".
		pos = 0
		 
		//Criando a pesquisa linear no array.
		enquanto((pos < 4) e (vet[pos] != num)){
			pos++	
		}

		//Condição para achar o número pesquisado no vetor
		se(vet[pos] == num){
			escreva ("\nO número que procuramos foi: ", num, "\n")
			escreva("Número encontrado na posição: ", pos, "\n")
		}
		senao{
			escreva ("\nO número que procuramos foi: ", num, "\n")
			escreva("O número procurado não existe no vetor. \n")
		}

		/* Imprimindo vetor completo na tela */
		escreva("\nO vetor completo é: ")
		para(i = 0; i < 5 ; i++){
			escreva(vet[i], ", ")
		}
		escreva("\n")
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20procurando%20o%20n%C3%BAmero%20digitado%20no%20vetor.por)

<br>

### Consideraremos um mercado que tenha 4 frutas em estoque (Morango, banana, caju e amora). Crie um algoritmo que pergunte a um usuário qual fruta ele deseja comprar. Caso a fruta esteja disponível, mostre uma mensagem de “fruta disponível”. Se não estiver disponível, mostre uma mensagem de fruta indisponível. PS: Armazene os nomes das frutas em um array
<details>
<summary>Código</summary>

```portugol
programa
{
	//Incluindo biblioteca de texto para converter as strings para minúsculo
	inclua biblioteca Texto --> t

	//Criando variáveis
	cadeia vet_frutas[] = {"morango", "banana", "caju", "amora"}
	cadeia fruta
	inteiro pos_vet
	
	funcao inicio()
	{
		escreva("Digite a fruta que deseja comprar: ")
		leia(fruta)

		//Fazendo o valor assimilado por "fruta" ficar sempre em caixa baixa
		fruta = t.caixa_baixa(fruta)

		//Criando lógica de pesquisa no array
		pos_vet = 0
		enquanto ((pos_vet < 3) e (vet_frutas[pos_vet] != fruta)){
			pos_vet ++
		}

		//Condição caso encontre ou não encontre a fruta pesquisada
		se (vet_frutas[pos_vet] == fruta){
			escreva("\nFruta disponivel ! \n")
		}
		senao{
			escreva("\nFruta acabou ! \n")
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20verifica%20disponibilidade%20de%20frutas.por)

<br>

<br>

[Voltar ao inicio](/README.md)