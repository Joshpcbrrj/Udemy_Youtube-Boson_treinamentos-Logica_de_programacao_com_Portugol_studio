# Recursividade (Funções recursivas)

## Conceituando
- Recursividade é definida como uma sub rotina que pode invocar a si mesma.
- Para resolver esse problema, a função se divide em 2 casos:
    - Base
    - Geral
<br>

- Uma função recursiva pode chamar a si mesmo de forma direta ou indireta
    - Direta → Quando invoca a si mesma diretamente.
    - indireta → Quando invoca a si mesma por meio de outra função.

### Como funciona a recursividade
- Uma função recursiva é invocada para resolver um problema.
- A função é capaz de resolver diretamente o caso mas simples do problema (Caso base).
    - Quando a função é chamada no caso base ela retorna um resultado.
<br>

- Se usarmos essa mesma função em um caso mais complexo, ela o divide em duas partes conceituais:
    - Uma que a função sabe resolver (caso base)
    - E uma que ela não sabe resolver (geral)

>**OBS:** Operação de recursividade
> - Para que a recursividade funcione, a parte que ela não sabe resolver  deve se parecer com o problema original, mas sendo ligeiramente menor ou mais simples que o problema em si.
> - A função chama a si própria para tentar resolver o problema maior (Chamada recursiva/ passo recursivo).
> - O passo recursivo é executado novamente, sobre uma versão mais simples do problema, até chegar ao **caso base**
>
> ### Representação
> <img src="/Arquivos/img/46.jpg" alt="Texto Alternativo" width="700">

<br>

## Projeto teste

### Calcule o fatorial de um número com uma função recursiva e mostre o resultado na tela
<details>
<summary>Código</summary>

```portugol
programa
{	
	//Criando variáveis
	inteiro num = 0
	inteiro fat = 0

	//Função para calculo de fatorial de forma recursiva
	funcao inteiro calcula_fatorial(inteiro n)
	{
		//Trabalhando caso  base 
		se((n == 0) ou (n == 1)){
			retorne 1	
		}
		
		//Trabalhando retorno recursivo (Chamando a propria função "calcula_fatorial")
		senao{
			retorne n * calcula_fatorial(n - 1)
		}
	}
	
	funcao inicio()
	{
		escreva("Calculo do fatorial de um número\n")

		//Laço para impedir que usuário digite número negativo
		enquanto(verdadeiro){
			escreva("Digite um número positivo: ")
			leia(num)
			se(num < 0){
				escreva("\nErro ! Só conseguimos calcular fatorial de números positivos.\n")	
			}
			senao{
				pare
			}
		}

		//Chamando a função recursiva para calcular o fatorial de um número
		fat =  calcula_fatorial(num)

		//Imprimindo resultado na tela
		escreva("O fatorial de : ", num, " é: ", fat, "\n")
		
		
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20fatorial%20com%20fun%C3%A7%C3%A3o%20recursiva.por)

<br>

<br>

[Voltar ao inicio](/README.md)