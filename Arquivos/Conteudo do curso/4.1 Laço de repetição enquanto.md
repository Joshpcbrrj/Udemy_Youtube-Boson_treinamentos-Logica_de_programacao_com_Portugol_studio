# Laço de Repetição “Enquanto”

## Conceituando
- Em muitos casos é necessário repetir um trecho de um programa diversas vezes. Usamos o “loop” para efetuar essa repetição quantas vezes forem necessárias para resolver o problema.
- Os “loops” também são chamados de “laços de repetição.
- Uma das estruturas mais usada para essa repetição é o enquanto.
    - Essa estrutura realiza teste lógico no início do loop, e cada vez que o teste retornar o valor verdadeiro, os comandos associados ao laço são novamente executados.
    - Quando o teste retorna falso o laço é encerrado e o processamento volta ao fluxo principal da aplicação.

> **OBS:** Sempre precisamos nos atentar para programar o retorno falso, senão teremos problema de looping infinito no nosso programa.

<br>

### Representação do enquanto em fluxograma:

<img src="/Arquivos/img/26.jpg" alt="Texto Alternativo" width="600">

#### Sintaxe de enquanto
```portugol
enquanto (condição){
	instruções executadas enquanto a condição continuar verdadeira
}
instruções executadas quando a condição se tornar falsa (Finaliza  laço)
```

<br>

<br>

## Projetos teste

### Escreva de 1 a 100
<details>
<summary>PDF</summary>

```portugol
programa
{

	inteiro num
	
	funcao inicio()
	{
		num = 1 //Atribuindo valor inicial da variável num

		enquanto (num <= 100){
			escreva("Número: ", num, "\n")
			num++ //Incremento para aumentar o valor de "num" a cada volta.  
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20escreva%20de%201%20a%20100.por)

<br>

### De bem vindo a x usuários

**1° Forma**
<details>
<summary>PDF</summary>

```portugol
programa
{

	cadeia nome_do_us
	
	funcao inicio()
	{
		escreva("Digite seu nome ou aperte X para parar o programa: ")
		leia(nome_do_us)

		enquanto (nome_do_us != "x"){
			escreva("\nBem vindo ", nome_do_us, " !")

			/* Solicitando mais dados a cada volta*/
			escreva("\nDigite seu nome ou aperte X para parar o programa: ")
			leia(nome_do_us)
		}
		escreva("\nAsta la vista, baby !")
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20de%20bem%20vindo%20a%20x%20usuarios.por)

**2° Forma**
<details>
<summary>PDF</summary>

```portugol
programa
{
	cadeia nome

	funcao inicio()
	{
		//Gerando looping infinito proposital
		enquanto(verdadeiro){
			escreva("Digite seu nome ou X para sair: ")
			leia (nome)
			se(nome == "x"){
				pare
			}
			escreva("\nSeja bem vindo: ",nome, "\n")
		}
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20de%20bem%20vindo%20a%20x%20usuarios%202.por)

<br>

### Refazendo o bem vindo a x usuários (com melhor estrutura para enquanto)
<details>
<summary>PDF</summary>

```portugol
programa
{
	cadeia nome_usuario
	
	funcao inicio()
	{
		enquanto (verdadeiro){
			escreva("Digite seu nome ou X para sair da execução: ")
			leia(nome_usuario)
			se (nome_usuario == "x"){
				pare //"Pare é igual ao brack (Vai trasformar a condição em "falsa"). 
			}
			senao{
				escreva("\nSeja bem vindo: ", nome_usuario, " !\n") 
			}
		}
		escreva("\nAté logo e volte sempre ! \n")
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20de%20bem%20vindo%20a%20x%20usuarios%20(Reestruturado).por)

<br>

<br>

[Voltar ao inicio](/README.md)
