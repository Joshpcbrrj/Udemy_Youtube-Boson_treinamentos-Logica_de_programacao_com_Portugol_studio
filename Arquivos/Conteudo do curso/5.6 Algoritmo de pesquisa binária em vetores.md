# Algoritmo de pesquisa binária em vetores

<details>
<summary>Página de cálculos Omni</summary>

[Omni calculator](https://www.omnicalculator.com/math/log-2)

</details>

<br>

## Destaques
- A pesquisa binária é um item procurado em uma lista ordenada. como temos ordenamento, a pesquisa se torna mais eficiente,
- Divide ao meio a parte da lista que pode conter o item (espaço de busca), comparando o elemento procurado com o elemento do meio da lista, até que sobre apenas um local possível para a busca.
- Aqui temos o paradigma “Dividindo para conquistar”.

>**OBS:** A busca linear só irá funcionar se o vetor analisado estiver ordenado de valor menor para o maior
<img src="/Arquivos/img/38.jpg" alt="Texto Alternativo" width="700">

### Representação exemplo de arvore binária
<img src="/Arquivos/img/39.jpg" alt="Texto Alternativo" width="600">

>**Note:**
> - Queremos achar o número 6, por exemplo.
> - A divisão do vetor é dada no número 4.
> - Se o 6 for maior que 4 procuraremos na direita, se não for procuraremos na esquerda.
> - Seguindo, com a procura a direita, vamos dividir o vetor no 7.
> - Como 6 é menor que 7, procuraremos na esquerda.
> - Lá esta o número 6, conforme vemos na figura (Usamos 3  passos para achar).

### Entendendo a sintaxe
Dado o array abaixo, como acharíamos o número 7 ?
<img src="/Arquivos/img/40.jpg" alt="Texto Alternativo" width="800">

```portugol
//Vamos precisar de 4 variaveis 
inicial = 0 //indica inicio do vetor
final = 9 // indica final do vetor
meio //Criamos a variável para definir depois qual será o meio.
encontrado = falso //Variavel flag para nos sinalizar se foi encontrado ou não.

//1° Vamos definir o meio 
meio = 4 

//2° Comparamos o valor 7 com o valor do meio. 
7 > 4 // Então procuraremos a direita do meio (4)

//3° Agora vamos mudar o valor de "Inicial", que será a primeira posição depois do meio (4)
inicial = 5 //Atribuindo novo inicio do vetor
final = 9 //A posição final neste caso se mantem

//4° Agora vamos atribuir o novo meio
meio = 7 //Atribuindo novo  meio do vetor

//5° Vamos começar comparando o número que queremos achar com esse novo meio
7 == 7 //Aqui já o podemos parar pois já achamos o 7.

//6° Como encotramos,a variável sinalizadora vai ser alterada para "verdadeiro"
encontrado = verdadeiro
```
<br>

## Projeto teste

### Crie um vetor de 10 posições com números aleatórios e organize do menor para o maior. Na sequencia, crie um algoritmo de pesquisa de um número (que será informado pelo usuário) e diga se ele esta ou não no vetor com números aleatórios
<details>
<summary>Código</summary>

```portugol
programa
{
	//Incluindo biblioteca de utilitários
	inclua biblioteca Util --> u

	//Criando variáveis
	inteiro cont, cont_a, cont_b
	inteiro aux
	inteiro vet[10]
	
	funcao inicio()
	{
		preenche_ordena_vetor(vet)

		//Imprimindo o vetor na tela (Para conferirmos os números presentes nele)
		para(cont_a = 0; cont_a <= 9; cont_a++){
			escreva (vet[cont_a] + " ")
		}
		
		//Pulando linha
		escreva("\n")

		//Implementando pesauisa binária
		inteiro inicial = 0 //Variável de inicio do vetor (Inicializada com 0)
		inteiro final = 9 //Variável de final do vetor (Inicializada  com a última posição do vetor (9) )
		inteiro meio //Variável do meio (Geralmente não inicializada)
		logico encontrado = falso //Variável flag de tipo lógico (Inicializada como falso poisainda não pesquisamos)
		inteiro busca //Variável que irá receber o número que queremos buscar (Geralmente atribuida pelo usuário)

		//Pedindo valor para "busca"
		escreva("Digite o valor que quer pesquisar: ")
		leia(busca)

		//Laço para encontrar o número da variável "busca"
		enquanto ((inicial <= final) e (nao encontrado)){
			meio = (inicial + final) / 2

			//Condição para comparação da variável "meio" com a variável "busca".
			se(vet[meio] == busca){
				encontrado = verdadeiro 
			}
			//Determinando para qual lado irei em caso de "meio" maior que "busca" (Movimenta final para esquerda)
			senao se (vet[meio] > busca){
				final = meio -1 //Deslocará o final do vetor para antes do meio
			}
			//Determinando para qual lado irei em caso de "meio" maior que "busca" (Movimenta inicio para direita)
			senao {
				inicial = meio + 1 //Deslocará o inicio do vetor para depois do meio
			}
		}

		//Agindo caso a nossa flag vire verdadeira (Caso encontremos o número da variável "busca").
		se(encontrado == verdadeiro){
			escreva("\nSim ! O número procurado se encontra no vetor.\n")
		}
		//Agindo caso a nossa flag permaneça falsa (Caso não encontremos o número da variável "busca")
		senao{
			escreva("\nNão ! o número procurado não esta presente no vetor.\n")
		}
		
	}

	//Criando função a parte para preencher e ordenar o vetor
	funcao preenche_ordena_vetor(inteiro vetor[]){
		//preenchendo vetor
		para (cont = 0; cont <= 9; cont++){
			vet[cont] = u.sorteia(1, 20)
		}
		
		//ordenando vetor
		para (cont_a = 0; cont_a <= 9; cont_a++){
			para(cont_b = cont_a + 1; cont_b <= 9; cont_b++){
				se(vetor[cont_a] > vetor[cont_b]){
					aux = vet[cont_b]
					vetor[cont_b] = vetor[cont_a]
					vetor[cont_a] = aux
				}
			}
		}
	}
	
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20pesquisa%20bin%C3%A1ria.por)

<br>

<br>

[Voltar ao inicio](/README.md)
