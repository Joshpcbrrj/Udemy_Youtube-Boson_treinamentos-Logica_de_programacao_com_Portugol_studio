# Sub rotinas (Funções e procedimentos)

## Conceituando
- Sub rotinas são blocos de código que definem tarefas que o programa pode usar várias vezes durante sua execução, podendo inclusive serem utilizados em outros programas.
- Sub rotinas podem ser utilizadas para cumprir tarefas específicas de um programa.
- Os comandos que definem as sub rotinas são escritos apenas uma vez, e  são escondidos  de outras rotinas.
- Os 2 tipos principais de sub rotinas são: Procedimentos e funções.
  
>**OBS:** Geralmente funções e procedimentos são criados no escopo global do programa.

### Função vs procedimento
| Procedimentos | Função |
| --- | --- |
| Sub rotina que executa uma tarefa sem retornar resultado ao chamador. É uma função sem retorno. | Sub rotina que executa uma tarefa, como o procedimento, porém retorna um resultado (valor) ao chamador. É uma função com retorno. |

<br>

### Sintaxe para criar sub rotina
```portugol
funcao[tipo_retorno] nome_da_funcao ([tipos_de_argumentos_necessarios])
{
	Código_da_funcao
	[retorne] valor_de_retorno //Caso necessário (Se tiver retorno  eu tenho que determinar o tipo)
}
```

### Chamada de sub rotina
- Após criar a função temos que invocar ela em outro local (Processo chamado de “call” ou invocação da  função).
- Lembrando que a sub rotina sempre apresenta informações, como argumentos, do que ela necessita  para realizar sua tarefa.

#### Invocando procedimentos
- O procedimento solicitará os argumentos que ele precisa para funcionar.
- Após realizar o processo, o programa continuará as execuções de código abaixo do procedimento.

    ```portugol
    nome_do_procedimento ([argumentos])
    ```
#### Invocando função 
- A função também irá solicitar os argumentos que ela precisa para funcionar.
- Como ela retorna valor, teremos que armazenar esse valor em algum lugar (Geralmente dentro de uma variável).
- O valor também pode ser retornado para outra função ou procedimento.

    ```portugol
    variável_de_gravação_para_retorno = nome_da_função ([argumentos])

    //OBS: Também vamos ter casos que o retorno da função é usado diretamente em outra função.
    ```
<br>

### Usando mais de um argumento na função
- Vamos separar  os argumentos por vírgula.
- Cada argumento deve ter seu tipo especificado.

Sintaxe
```portugol
funcao nome_da_função (tipo arg1, tipo arg2, tipo arg3, tipo argn)
{
	Código_da_função
}
```
<br>

### Trabalhando com funções com retorno
- Permite retornar ao chamador um valor gerado dentro de uma sub rotina, que será a chamada de função.
- Esse retorno diferencia uma função sem retorno (Procedimento) de uma função (função com retorno).
- O retorno da função deve ser gravado em algum lugar (geralmente dentro de uma variável).
- O retorno deve ter um tipo especificado (numero, caractere, cadeia  etc).
- O retorno é a última coisa a ser executada na nossa função com retorno.

Sintaxe
```portugol
funcao tipo_de_retorno nome_da_função ([tipo argumento])
{
	Código_da_função
	retorno valor_a_ser_retornado
}
```

<br>

## Projetos teste

### Crie uma sub rotina (função sem retorno) que escreva uma frase simples
<details>
<summary>Código</summary>

```portugol
programa
{
	//Criando função simples (procedimento sem retorno)
	funcao mostra_mensagem(){
		escreva("Uma vez Flamengo, sempre Flamengo !!! \n")
		escreva("Vamo que vamo, programação. \n")
	}
	
	
	funcao inicio()
	{
		//Chamando função criada
		mostra_mensagem()
	}

	
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20Sub%20rotina%20sem%20retorno.por)

<br>

### Crie uma função que calcule uma potencia fornecida sobre um número também fornecido
<details>
<summary>Código</summary>

```portugol
programa
{
	//Criando função para calculo de potencia de um número
	funcao calcula_potencia(inteiro num_base, inteiro num_potencia){
		// Criando variáveis
		inteiro i //Interador para laço para
		inteiro resultado = 1 //Atribuindo valor inicial para "resultado".
		 
		// Criando laço de repetição para resolver potenciação 
		para (i = 0; i < num_potencia; i++){
			 resultado = num_base * resultado
		}
		//Escrevendo resultado
		escreva("A base é: ", num_base, ", ", "a potencia é: ",num_potencia, ".\n")
		escreva("O resultado da potenciação é: ", resultado, "\n")
	}

	
	funcao inicio()
	{	//Criando variáveis e pedindo valores para calculo.
		inteiro num_1, num_2
		escreva("Digite um número para base: ")
		leia(num_1)
		escreva("Digite um número para potencia: ")
		leia(num_2)

		//Só pulando uma linha para  organizar o resultado.
		escreva("\n") 

		//Chamando a função "calcula_potencia".
		calcula_potencia(num_1,num_2)
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20potencia%C3%A7%C3%A3o%20de%20um%20n%C3%BAmero.por)

<br>

### Peça dados para altura e largura e calcule a area de um terreno
<details>
<summary>Código</summary>

```portugol
programa
{
	/* Criando função com o retorno de area de um determinado espaço*/
	funcao real calcula_area(real largura, real comprimento){
		real area = 0.0
		area = largura * comprimento

		retorne area
	}
		
	funcao inicio()
	{	
		/* Criando e pedindo inputs do terreno para calcular area posteriormente*/
		real larg = 0.0
		real comp = 0.0
		real area_total = 0.0
		escreva("Digite a largura do terreno: ")
		leia(larg)
		escreva("Digite o comprimento do terreno: ")
		leia(comp)

		// Retornando procedimento da função "calcula_area" para dentro da variável "area_total"
		area_total = calcula_area(larg, comp)

		// Escrevendo os dados na tela
		escreva("\nA area total do terreno é: ", area_total, " metros quadrados.\n")
		
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20calculando%20area%20de%20um%20terreno.por)

<br>

### Trabalhando com função sem retorno com mais de um argumento. Crie um software que peça um número inicial e final e mostre todo o range entre eles
<details>
<summary>Código</summary>

```portugol
programa
{
	//Criando variáveis
	inteiro num_inicial, num_final
	
	//Criando função com múltiplos argumentos (Mostra números)
	funcao contar_numeros(inteiro x, inteiro y)
	{
		para(inteiro i = x; i <= y; i++){
			se(i == y){
				escreva(i, "\n")	
			}
			senao
				escreva(i, ", ")
		}
	}
	
	
	funcao inicio()
	{
		//Solicitando número inicial e final
		escreva("Digite o número inicial: ")
		leia(num_inicial)
		escreva("Digite o número final: ")
		leia(num_final)
	
		//Limpando a tela e mostrando mensagem
		limpa()
		escreva("Mostrando números do inicial ao final:\n")
		
		//Invocando a função "contar_numeros" para exibir os números de inicial a final
		contar_numeros(num_inicial, num_final)
		

	}

	
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20mostrando%20n%C3%BAmeros%20do%20inicial%20ao%20final.por)

<br>

### Crie um programa que use uma função para calcular e retornar a área de um círculo, solicitando ao usuário o valor do raio (r) da circunferência. A área do circulo é dada por: $A = \pi * r^2$
<details>
<summary>Código</summary>

```portugol
programa
{
	//Incluindo biblioteca matemática (Para pegar o valor de PI)
	inclua biblioteca Matematica --> m

	//Criando variáveis
	real raio
	real area_total

	//Função para calcular a área de uma circunferencia
	funcao real calcula_area (real r)
	{
		real area_circulo = 0.0
		area_circulo = m.PI *r *r
		retorne area_circulo
	}
	
	funcao inicio()
	{
		escreva("Digite o raio do circulo em cm: ")
		leia(raio)

		//Retornando o valor da função "calcula_area" para a variável "area_total" (Usando raio digitado como parametro)
		area_total = calcula_area(raio)

		//Escrevendo a area do circulo na tela
		escreva("A area do círculo é: ", area_total, "cm^2\n")
	}
}
```

</details>

[Arquivo do código em Portugol](/Arquivos/C%C3%B3digo/Teste%20calculo%20da%20%C3%A1rea%20do%20c%C3%ADrculo.por)

<br>

<br>

[Voltar ao inicio](/README.md)